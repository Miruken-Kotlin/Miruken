apply from: "${rootDir}/gradle/kotlin.gradle"

dependencies {
    testCompile group: 'io.github.classgraph', name: 'classgraph', version: '4.4.7'
    testCompile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.7'
    testCompile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.7'
}

configurations {
    testClasses {
        extendsFrom(testRuntime)
    }
}

task testJar(type: Jar) {
    classifier = 'test'
    from sourceSets.test.output
}

artifacts {
    testClasses testJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId 'com.miruken'
            artifactId 'miruken'
            pom.withXml {
                def properties = asNode().appendNode('properties')
                properties.appendNode('maven.compiler.source', '1.8')
                properties.appendNode('maven.compiler.target', '1.8')
            }
        }
    }
}

bintray {
    user         = System.getenv('BINTRAY_USER')
    key          = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish      = true
    pkg {
        userOrg  = 'Miruken'
        repo     = 'MavenPackages'
        name     = 'miruken'
        userOrg  = 'miruken'
        licenses = ['MIT']
        vcsUrl   = 'https://github.com/Miruken-Kotlin/Miruken.git'
        version {
            released = new Date()
        }
    }
}
